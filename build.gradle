buildscript {
    ext.kotlin_version = '1.0.3'
    ext.dokka_version = '0.9.9'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

group 'org.rs3vans'
version '1.0.2-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "org.slf4j:slf4j-api:1.7.21"

    testCompile "org.slf4j:slf4j-simple:1.7.21"
    testCompile "junit:junit:4.7"
}

dokka {
    moduleName = 'klogger'
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    processConfigurations = ['compile']
    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/cy6erGn0m/vertx3-lang-kotlin/blob/master/src/main/kotlin"
        suffix = "#L"
    }
    sourceDirs = files('src/main/kotlin')
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['KloggerPublication']
    pkg {
        repo = 'maven'
        name = 'klogger'
        userOrg = user
        desc = 'A Kotlin library for logging.'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/rs3vans/klogger/wiki'
        issueTrackerUrl = 'https://github.com/rs3vans/klogger/issues'
        vcsUrl = 'https://github.com/rs3vans/klogger.git'
        publicDownloadNumbers = true
        githubRepo = 'rs3vans/klogger'
        version {
            name = project.version
            desc = 'Klogger - ' + project.version
            vcsTag = project.version
        }
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task dokkaJar(type: Jar, dependsOn: dokkaJavadoc) {
    from dokkaJavadoc.outputDirectory
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar, dokkaJar
}

publishing {
    publications {
        KloggerPublication(MavenPublication) {
            from components.java
            groupId 'com.github.rs3vans.klogger'
            artifactId 'klogger'
            version project.version
            artifact sourcesJar
            artifact dokkaJar
        }
    }
}